class public Inscripcion

	method public void service(BBjspServletContext context!)
	
		use ::utils.bbj::Login
		use ::utils.bbj::Socio
		use ::emailutil.bbj::Email

        declare BBjspWebRequest request!
        declare BBjspWebResponse response!
        declare Login login!

		request! = context!.getRequest()
		response! = context!.getResponse()
		response!.setContentType("application/json")

		Rem Default es que no hay data valida
		e$="{""errors"": { ""global"": ""Informacion Invalida. \nContacte a su cooperativa.""} }"
		status=400

		body!=request!.getBody()

		if body! = null() then 
			java.lang.System.err.println("No body found in request. ")
			goto salida
		endif

		java.lang.System.out.println("Body: "+body!)
		jsonO!= new org.json.JSONObject(body!)
		cuenta!=jsonO!.get("credentials").get("cuenta").toString(err=*next)
		cuenta!= STR(NUM(cuenta!):"000000000")
		email!=jsonO!.get("credentials").get("email").toString(err=*next)
		segsoc!=jsonO!.get("credentials").get("segsoc").toString(err=*next)
		password!=jsonO!.get("credentials").get("password").toString(err=*next)

		java.lang.System.out.println("Pedido de inscripcion usando cuenta "+cuenta!)

		REM Verifica si existe  un login con ese email.
		login! = new Login(email!)
		if login!.getexiste() then
			java.lang.System.err.println("Error en inscripcion: Email ya existen en LOGIN")
			goto salida
		endif

		REM Verifica si existe una cuenta valida.
		socio! = new Socio(cuenta!)
		if !socio!.getexiste() then 
			java.lang.System.err.println("Error en inscripcion: Numero de cuenta no existe")
			goto salida
		endif

		REM Verifica si el SS es valido.
		ssn$=socio!.getssn()
		if ssn$(6,4)<>segsoc! then 
			java.lang.System.err.println("Error en inscripcion: Numero de segsoc incorrecto")
			goto salida
		endif

		REM Verifica si el email es valido.
		email$=socio!.getemail()
		if email$<>email! then 
			java.lang.System.err.println("Error en inscripcion: Email incorrecto")
			goto salida
		endif

		REM Crear entrada en tabla Login
		if login!.crear(cuenta!, email!, password!) then
			if Email.enviarEmailInscripcion(login!.getemail(), login!.gettoken()) then 
				java.lang.System.out.println("Email para confirmar inscripcion enviado. Entrada en login creada.")
				status=204
			else 
				java.lang.System.err.println("Envio de email fallo. Se considera que la transaccion no es valida para cuenta: "+cuenta$)
				rem Aqui poner logica para manejar este caso
			endif
		endif

		salida:
		if status<>204 then response!.getOutputStream().write(e$)
		response!.setStatus(status)

	methodend

classend

