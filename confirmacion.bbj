class public Confirmacion

	method public void service(BBjspServletContext context!)
	
		declare BBjspWebRequest request!
		declare BBjspWebResponse response!
		
		use java.util.HashMap
		use ::jwt.bbj::Jwt
		use ::utils.bbj::Login

		request! = context!.getRequest()
		response! = context!.getResponse()
		path! = request!.getPathInfo()

		Rem Default es que no hay data valida
		res$="<html><h2>No se pudo confirmar su informacion. Contacte a su cooperativa.</h2></html>"
		status=400

		token! = path!.replace("/","")
		java.lang.System.out.println("Confirmacion token recibido: "+token!)

		REM Verificar token para sacar detalles del cliente
		claims! = Jwt.verify(token!)
		email$=claims!.get("email",err=*next)
		java.lang.System.out.println(pgm(1)+" Email: "+email$)

		if email$="" then 
			java.lang.System.err.println(pgm(1)+"No se encontro Email.")
			response!.getOutputStream().write(res$)
			response!.setStatus(status)
			goto salida
		endif

		login! = new Login(email$)

		if !login!.getexiste() then 
			java.lang.System.err.println(pgm(1)+"No se encontro Login.")
			response!.getOutputStream().write(res$)
			response!.setStatus(status)
			goto salida
		endif

		if login!.confirmar() then		
			status = 200
			res$="<html><h2>Confirmacion Exitosa. Intente hacer login en su app.</h2></html>"
		endif

		response!.getOutputStream().write(res$)
		response!.setStatus(status)

	salida:
	methodend

classend
