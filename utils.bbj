REM /**
REM  * Utils.bbj
REM  * @author admin
REM  *
REM  */

class public Login

	field public BBjString cuenta!
	field public BBjString email!
	field public BBjString hashed!
	field public BBjString token!
	field public BBjInt confirmado!
	field public BBjInt uid!
	field public BBjInt existe!
	field public BBjInt intentos!

	use ::jwt.bbj::Jwt
	use org.mindrot.jbcrypt.BCrypt

	method public Login(BBjString email!)
		#email!=email!
		#existe!=0
		while 1
			SQLChan = SQLunt
			SQLOpen(SQLChan)"NcusDB"
			SQLPrep(SQLChan)"select uid, cuenta, token, confirmado,password, intentos from login where email=?"
			SQLExec(SQLChan,err=*break)email!
			dim login$:SQLTMPL(SQLChan)
			login$=SQLFetch(SQLChan,err=*break)
			#uid!=login.uid
			#cuenta!=login.cuenta$
			#token!=login.token$
			#confirmado!=login.confirmado
			#intentos!=login.intentos
			#hashed!=CVS(login.password$,3)
			#existe!=1
			break
		wend
		SQLClose(SQLChan)
	methodend

	method public Boolean bloqueado()
		SQLChan = SQLunt
		SQLOpen(SQLChan)"NcusDB"
		SQLPrep(SQLChan)"select intentos from login where uid=?"
		SQLExec(SQLChan,err=*break)#uid!
		dim login$:SQLTMPL(SQLChan)
		login$=SQLFetch(SQLChan,err=*break)
		intentos=login.intentos
		SQLClose(SQLChan)
		if intentos>3 then 
			methodret Boolean.TRUE 
		else
			methodret Boolean.FALSE
		endif
	methodend

	method public Boolean crear(BBjString cuenta!, BBjString email!, BBjString password!)
		params! = new java.util.HashMap()
		params!.put("email", email!)
		params!.put("confirmado","")
		jwt! = Jwt.sign(params!,0)
		hashed! = BCrypt.hashpw(password!, BCrypt.gensalt())
		while 1
			sql$="insert into login (uid, cuenta, email, password, token) values (login_uid.nextval, '"+cuenta!+"','"+email!+"','"+hashed!+"','"+jwt!+"')"
			java.lang.System.out.println(sql$)
			SQLChan = SQLunt
			SQLOpen(SQLChan)"NcusDB"
			SQLPrep(SQLChan)sql$
			SQLExec(SQLChan,err=*break)
			SQLClose(SQLChan)
			#cuenta!=cuenta!
			#email!=email!
			#token!=CAST(BBjString,jwt!)
			methodret Boolean.TRUE
		wend
		SQLClose(SQLChan)
		methodret Boolean.FALSE
	methodend

	method public Boolean confirmar()
		params! = new java.util.HashMap()
		params!.put("email", #email!)
		params!.put("confirmado","true")
		jwt! = Jwt.sign(params!,0)
		while 1
			sql$="update login set confirmado=1, token='"+jwt!+"' where email=?"
			SQLChan = SQLunt
			SQLOpen(SQLChan)"NcusDB"
			SQLPrep(SQLChan)sql$
			SQLExec(SQLChan,err=*break)#email!
			SQLClose(SQLChan)
			methodret Boolean.TRUE
			break
		wend
		SQLClose(SQLChan)
		methodret Boolean.FALSE
	methodend

	method public Boolean cambiopassword(BBjString password!)
		hashed! = BCrypt.hashpw(password!, BCrypt.gensalt())
		while 1
			sql$="update login set password='"+hashed!+"' where email=?"
			SQLChan = SQLunt
			SQLOpen(SQLChan)"NcusDB"
			SQLPrep(SQLChan)sql$
			SQLExec(SQLChan,err=*break)#email!
			SQLClose(SQLChan)
			methodret Boolean.TRUE
			break
		wend
		SQLClose(SQLChan)
		methodret Boolean.FALSE
	methodend

	method public Boolean passwordValido(BBjString password!)
		if BCrypt.checkpw(password!, #hashed!) then
			sql$="update login set intentos=0, lastlogin='"+date(0:"%Yl-%Mz-%Dz")+"' where uid=?"
			SQLChan = SQLunt
			SQLOpen(SQLChan)"NcusDB"
			SQLPrep(SQLChan)sql$
			SQLExec(SQLChan,err=*break)#uid!
			SQLClose(SQLChan)
			methodret Boolean.TRUE
		else
			sql$="update login set intentos=intentos+1, lastlogin='"+date(0:"%Yl-%Mz-%Dz")+"' where uid=?"
			SQLChan = SQLunt
			SQLOpen(SQLChan)"NcusDB"
			SQLPrep(SQLChan)sql$
			SQLExec(SQLChan,err=*break)#uid!
			SQLClose(SQLChan)
			methodret Boolean.FALSE
		endif
	methodend

classend

class public Socio

	field public BBjString cuenta!
	field public BBjString ssn!
	field public BBjString email!
	field public BBjInt existe!

	method public Socio(BBjString cuenta!)
		#cuenta!=cuenta!
		#existe!=0
		while 1
			SQLChan = SQLunt
			SQLOpen(SQLChan)"NcusDB"
			SQLPrep(SQLChan)"select ssn, email from SOCIOS where cuenta=? and swcerrada=0 and swfallecido=0"
			SQLExec(SQLChan,err=*break)cuenta!
			dim socios$:SQLTMPL(SQLChan)
			socios$=SQLFetch(SQLChan,err=*break)
			#ssn!=socios.ssn$
			#email!=socios.email$
			#existe!=1
			break
		wend
		SQLClose(SQLChan)
	methodend
	
classend
