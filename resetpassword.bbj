class public Resetpassword

	method public void service(BBjspServletContext context!)

		declare BBjspWebRequest request!
		declare BBjspWebResponse response!

		use ::jwt.bbj::Jwt
		use ::utils.bbj::Login

		request! = context!.getRequest()
		response! = context!.getResponse()
		path! = request!.getPathInfo()

		Rem Default es que no hay data valida
		res$="<html><h2>No se pudo confirmar su informacion. Contacte a su cooperativa.</h2></html>"
		status=400

		token! = path!.replace("/","")
		java.lang.System.out.println("Confirmacion token recibido: "+token!)

		REM Verificar token para sacar detalles del cliente
		claims! = Jwt.verify(token!)

		if claims!= null() then
			REM Token expirado. No se puede cambiar el pwd.
			java.lang.System.out.println(pgm(1)+"Token expirado.")
			response!.getOutputStream().write("<html><h2>El pedido de cambio de password ya no esta vigente.</h2></html>")
			response!.setStatus(status)
			goto salida
		endif

		email$=claims!.get("email",err=*next)
		java.lang.System.out.println(pgm(1)+" Email: "+email$)

		if email$="" then 
			java.lang.System.out.println(pgm(1)+"No se encontro Email.")
			response!.getOutputStream().write(res$)
			response!.setStatus(status)
			goto salida
		endif

		login! = new Login(email$)

		if login!.getexiste() then
			redirectURL$ = response!.encodeRedirectURL("http://127.0.0.1:8888/apps/resetpassword?email="+CVS(email$,3))
			response!.sendRedirect(redirectURL$)
			goto salida
		else
			java.lang.System.out.println(pgm(1)+"No se encontro Login.")
			response!.getOutputStream().write(res$)
			response!.setStatus(status)
			goto salida
		endif

		response!.getOutputStream().write(res$)
		response!.setStatus(status)

	salida:
	methodend

classend
