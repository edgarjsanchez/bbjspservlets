class public Login

	method public void service(BBjspServletContext context!)

		use ::utils.bbj::Login
		use ::jwt.bbj::Jwt

		declare BBjspWebRequest request!
		declare BBjspWebResponse response!
		declare Login login!

		request! = context!.getRequest()
		response! = context!.getResponse()
		response!.setContentType("application/json")

        Rem Default es que no hay data valida
        e$="{""errors"": { ""global"": ""Credenciales Invalidas.""} }"
        status=400

		REM Verificar que el pedido venga de programacion autorizada (Compania dentro de token)
		auth! = request!.getHeader("Authorization",err=*next)
		if auth! <> null() then
			auth$=auth!
			jwt$ = auth$(POS(" "=auth$)+1)
			java.lang.System.out.println("Auth: "+jwt$)
		else
			java.lang.System.err.println("Auth: NULO")
			rem 403 - Forbidden - Authentication failure or invalid Application ID.
			e$="{""errors"": { ""global"": ""No Autorizado.""} }"
			status=403
			goto salida
		endif

		claims! = Jwt.verify(jwt$)
		compania$=claims!.get("compania",err=*next)
		if compania$<>"MySoftwareOV" then
			rem 403 - Forbidden - Authentication failure or invalid Application ID.
			e$="{""errors"": { ""global"": ""No Autorizado.""} }"
			status=403
			goto salida
		endif
		
		body!=request!.getBody()
		if body! = null() then 
			java.lang.System.err.println("No body found in request. ")
			rem 400 - Bad request - Malformed syntax or a bad query.
			e$="{""errors"": { ""global"": ""Informacion Invalida.""} }"
			status=400
			goto salida
		endif

		java.lang.System.out.println("Body: "+body!)
		jsonO!= new org.json.JSONObject(body!)
		user!=jsonO!.get("credentials").get("user").toString(err=*next)
		pwd!=jsonO!.get("credentials").get("password").toString(err=*next)
		java.lang.System.out.println("Login request using "+user!)

		login! = new Login(user!)

		if login!.getexiste() and login!.getconfirmado() then
			if login!.bloqueado() then
				status=400
				e$="{""errors"": { ""global"": ""Por razones de seguridad su cuenta fue bloqueada. Contacte a su cooperativa.""} }"
			else
				if login!.passwordValido(pwd!) then
					status=200
					e$="{""user"":{""email"":"""+ user! +""", ""confirmed"":""true"",""token"":"""+login!.gettoken()+"""}}"
				else
					status=400
					if login!.getintentos()=3 then 
						e$="{""errors"": { ""global"": ""Credenciales Invalidas. Si falla un intento mas su cuenta sera bloqueda.""} }"
					else
						e$="{""errors"": { ""global"": ""Credenciales Invalidas.""} }"
					endif
				endif
			endif 
		endif

		salida:
		java.lang.System.out.println(STR(status)+" Message returned "+e$)
		response!.getOutputStream().write(e$)
		response!.setStatus(status)

	methodend
	
classend
